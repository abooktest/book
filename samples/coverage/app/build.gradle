apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.book.codecoverage"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

apply plugin: 'jacoco'  // applying gradle jacoco plugin
task codeCoverageReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true // export xml report
        html.enabled = true // export html report
    }
    def classIncluded = [ '**/*.class' ]
    def classExcluded = [ '**/R.class', '**/R$*.class', '**/BuildConfig.*' ]
    classDirectories = fileTree(
            dir: 'build/intermediates/classes/debug',
            includes: classIncluded,
            excludes: classExcluded
    )
    sourceDirectories = files('src/main/java')  // specify source directory
    executionData = files('build/jacoco/testDebugUnitTest.exec') // specify execution data
}

task getCoverage(type: Exec, dependsOn: 'codeCoverageReport') {
    group = "Reporting"
    commandLine "xdg-open", "$buildDir/reports/jacoco/codeCoverageReport/html/index.html"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
